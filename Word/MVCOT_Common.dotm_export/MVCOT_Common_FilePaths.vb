Attribute VB_Name = "FilePaths"
'Public Const strUserIniDefault = "C:\TempTao\MVCOT\MSD templates"

Public Const BRANDING_LOGO_BKM = "bkmBrandingLogo"
Public Const BRANDING_TEXT_BKM = "bkmBrandingAddress"
Public Const MAX_INI_WRITE_ATTEMPTS = 3
Public Const IMG_EXTENSION = ".jpg"

Public Const FILE_VERSION = "3.2"
Public Const LAST_UPDATED = "13/03/2017 5:00pm"
Public Const LAST_AUTHOR = "Allfields"

Public BASE_PATH As String
Public TOOLKIT_LOADED As Boolean

Public strCoreTemplatesPath As String
Public strOtherTemplatesPath As String
Public strSpecialTemplatesPath As String
Public strWorkunitLogosPath As String
Public strMSDGlobalPath As String
Public strAddressConfigPath As String
Public strUserIniPath As String
Public strUserIni As String
Public strImagePath As String

Public IniOP As INIHelper

Sub Autoexec()

    If Not TOOLKIT_LOADED Then
        ' Get the default path from the Workgroup Templates directive in Word
        BASE_PATH = Options.DefaultFilePath(wdWorkgroupTemplatesPath)
        
        strCoreTemplatesPath = BASE_PATH & "\Templates"
        strOtherTemplatesPath = BASE_PATH & "\Other Templates"
        strSpecialTemplatesPath = BASE_PATH & "\Special Templates"
        strWorkunitLogosPath = BASE_PATH & "\Workunit Logos"
        strMSDGlobalPath = BASE_PATH & "\MVCOTGlobal.ini"
        strAddressConfigPath = BASE_PATH & "\Addresses.ini"
        strUserIniPath = "H:\Template" 'Options.DefaultFilePath(wdUserTemplatesPath)
        strUserIni = strUserIniPath & "\User.ini"
        strImagePath = strCoreTemplatesPath
        
        Set IniOP = New INIHelper
        TOOLKIT_LOADED = True
    End If
    
End Sub

Function CheckRequirements() As Boolean
    
    If Not FolderExists(strUserIniPath) Then _
        ThrowFatalError "The directory to store the User configuration doesnt exist" & vbCr & _
                            "Looking in: " & strUserIniPath
    

    
    If Not FileExists(strMSDGlobalPath) Then _
        ThrowFatalError "MSD Global Configuration not found." & vbCr & _
                            "Looking in: " & strMSDGlobalPath
    
    If Not FolderExists(strCoreTemplatesPath) Then _
        ThrowFatalError "There does not appear to be a folder for Core templates." & vbCr & _
                            "Looking in: " & strCoreTemplatesPath
        
        ' Check there is a existing user INI
    Dim MakeProfile As Boolean
    MakeProfile = False
    If Not FileExists(strUserIni) Then
        MakeProfile = True
    Else
        Dim aUsers
        aUsers = IniOP.LoadIniSectionsArray(strUserIni)
        MakeProfile = UBound(aUsers) < 0
    End If
       
    If MakeProfile Then
        MsgBox "No user profile was found. Please spend some time setting up a new profile", vbOK + vbInformation
        CreateNewUserIni
        LaunchProfileSetup "_none"
    End If
    
    CheckRequirements = MakeProfile
    
End Function

Private Sub CreateNewUserIni()

    Dim fso As FileSystemObject
    Dim FSOFile As TextStream
    Set fso = New FileSystemObject
    If Not FileExists(strUserIni) Then
        NewIniPath = strUserIni
        Set FSOFile = fso.OpenTextFile(NewIniPath, 2, True)
        With FSOFile
            .WriteLine ("; User INI Configuration file for MVCOT.")
            .WriteLine ("; Generated on " & Format(Date, "dd/mm/yyyy") & " by the Template Toolkit")
            .WriteLine ("; This file is edited by the Template Toolkit, do not edit this file directly")
            .WriteBlankLines (2)
            .Close
        End With
    End If

End Sub
